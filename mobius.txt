#c computes conjugacy classes of subgroups
c:=function(n,G)
	local cc;
	cc:=ConjugacyClassesSubgroups(G);
	return ConjugateSubgroups(G,Representative(cc[n]));
	end;;

#computes whether a subgroup H is in K
H_in_K:=function(H,K)
	if IsSubgroup(K,H)=true then
		return 1;
	else
		return 0;
	fi;
end;;

#computes whether a conjugacy class C_i is in C_j
f:=function(C_i,C_j)
	local i;
	H_1:=C_i[1];
	for i in C_j do
		if H_in_K(H_1,i)=1 then
			return 1;
		fi;
	od;
	return 0;
end;;	

#computes each row of the zeta function for the conjugacy classes of G
row_conj:=function(i,G)
	local list,j,cc;
	list:=[ ];
	j:=1;
	cc:=Size(ConjugacyClassesSubgroups(G));
	while j<=cc do
		Add(list,f(c(i,G),c(j,G)));
		j:=j+1;
	od;
	return list;
end;;

#puts each row together into a matrix
zeta_conj:=function(G)
	local i, zeta_mat,cc;
	zeta_mat:=[ ];
	i:=1;
	cc:=Size(ConjugacyClassesSubgroups(G));
	while i<=cc do
		Add(zeta_mat,row_conj(i,G));
		i:=i+1;
	od;
	return zeta_mat;
end;;

#takes the inverse of the zeta function to compute the mobius function
mobius_conj:=function(G)
	local s,mobius_mat;
	s:=Size(ConjugacyClassesSubgroups(G));
	mobius_mat:=Inverse(zeta_conj(G));
	return (mobius_mat[1])[s];
end;;

#computes subgroups of G	
sub:=function(G)
	local list,i,cc;
	list:=[ ];
	i:=1;
	cc:=Size(ConjugacyClassesSubgroups(G));
	while i<=cc do
		Append(list, c(i,G));
		i:=i+1;
	od;
	return list;
end;;

#computes the rows of the zeta function matrix for subgroups
row_sub:=function(i,G)
	local list,j,sub_g;
	list:=[ ];
	sub_g:=sub(G);
	for j in sub_g do
		Add(list,H_in_K(sub_g[i],j));
	od;
	return list;
end;;

#puts these together to create the zeta matrix
zeta_sub:=function(G)
	local i, zeta_mat,size_sub;
	size_sub:=Size(sub(G));
	zeta_mat:=[ ];
	i:=1;
	while i<=size_sub do
		Add(zeta_mat,row_sub(i,G));
		i:=i+1;
	od;
	return zeta_mat;
end;;

#takes inverse to compute mobius function
mobius_sub:=function(G)
	local s,mobius_mat;
	s:=Size(sub(G));
	mobius_mat:=Inverse(zeta_sub(G));
	return (mobius_mat[1])[s];
end;;

#computes the size of the derived subgroup
d_sub:=function(G)
	return Size(DerivedSubgroup(G));
end;;

#checks whether mobius_s(G)=G'*mobius_c(G)
check:=function(G)
	if mobius_sub(G)=d_sub(G)*mobius_conj(G) then
		return true;
	else
		return false;
	fi;
end;;

